' Gambas class file

Public Const State_InHeader As Short = 0
Public Const State_InChunk As Short = 1

Static Public Function ReadFile(FileName As String) As WavFile

  Dim Model As New WavFile
  Dim State As Short = State_InHeader
  Dim DataFile As File
  Dim FormatChunk As WavChunkFormat
  Dim DataChunk As WavChunkData
  Dim ChunkId As String

  DataFile = Open FileName For Read

  Do While Not Eof(DataFile)
    Select Case State

      Case State_InHeader
        Model.GroupID = ReadString(DataFile, 4)
        Model.FileLength = ReadInteger(DataFile, 4)
        Model.RiffType = ReadString(DataFile, 4)
        State = State_InChunk

      Case State_InChunk
        ChunkId = Trim(Lower(ReadString(DataFile, 4)))
        Select Case ChunkId

          Case "fmt"
            FormatChunk = New WavChunkFormat
            With FormatChunk
              .ChunkID = "fmt"
              .ChunkSize = ReadInteger(DataFile, 4)
              .FormatTag = ReadInteger(DataFile, 2)
              .Channels = ReadInteger(DataFile, 2)
              .SamplesPerSec = ReadInteger(DataFile, 4)
              .AvgBytesPerSec = ReadInteger(DataFile, 4)
              .BlockAlign = ReadInteger(DataFile, 2)
              .BitsPerSample = ReadInteger(DataFile, 4)
            End With
            Model.FormatChunks.Add(FormatChunk)

          Case "data"
            DataChunk = New WavChunkData
            With DataChunk
              .ChunkID = "data"
              .ChunkSize = ReadInteger(DataFile, 4)

            End With
            Model.DataChunks.Add(DataChunk)

          Case Else
            Print "Found: Chunk Id: " & ChunkId

        End Select

    End Select
  Loop

  Return Model

End

Static Private Function ReadString(OpenFile As File, Length As Integer) As String

  Dim ReturnValue As String
  Dim Data As Byte
  Dim x As Integer

  For x = 1 To Length
    Read #OpenFile, Data
    ReturnValue &= String.Chr(Data)
  Next
  Return ReturnValue

End

Static Private Function ReadInteger(OpenFile As File, Length As Integer) As Integer

  Dim ReturnValue As Integer
  Dim Data As Byte
  Dim x As Integer

  For x = 0 To Length - 1
    Read #OpenFile, Data
    If x = 0 Then
      Returnvalue = Data
    Else
      ReturnValue += Data * (2 ^ (8 * x))
    Endif
  Next
  Return ReturnValue

End
