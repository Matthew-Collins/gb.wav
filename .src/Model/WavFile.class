' Gambas class file

Public GroupID As String 'Surprisingly enough, this Is Always "RIFF"
Public FileLength As Long 'File length In bytes, measured From offset 8
Public RiffType As String 'In wave files, this Is Always "WAVE"

Public FormatChunks As New WavChunkFormat[]
Public DataChunks As New WavChunkData[]
Public FactChunks As New WavChunkFact[]

Static Public Function GetNewTemplate() As WavFile

  Dim Model As New WavFile

  Model.GroupID = "RIFF"
  Model.RiffType = "WAVE"

  Model.FormatChunks.Add(WavChunkFormat.GetNewTemplate())
  Model.DataChunks.Add(WavChunkData.GetNewTemplate())

  Return Model

End

Public Function NewFileLength() As Long

  Dim ReturnValue As Long
  Dim FormatChunk As WavChunkFormat
  Dim DataChunk As WavChunkData
  Dim FactChunk As WavChunkFact

  ReturnValue += 4

  For Each FormatChunk In Me.FormatChunks
    ReturnValue += 24
  Next

  For Each DataChunk In Me.DataChunks
    ReturnValue += DataChunk.NewChunkSize() + 8
  Next

  For Each FactChunk In Me.FactChunks
    ReturnValue += 12
  Next

  Return ReturnValue

End

Public Sub Mute_Stereo_Left()

  Dim DataChunk As WavChunkData
  Dim x As Integer

  For Each DataChunk In Me.DataChunks
    For x = 0 To DataChunk.ShortArray.Count - 1 Step 2
      DataChunk.ShortArray[x] = 0
    Next
  Next

End

Public Sub Mute_Stereo_Right()

  Dim DataChunk As WavChunkData
  Dim x As Integer

  For Each DataChunk In Me.DataChunks
    For x = 1 To DataChunk.ShortArray.Count - 1 Step 2
      DataChunk.ShortArray[x] = 0
    Next
  Next

End

Public Sub AppendWavFile(FileName As String)

  Dim NewFile As WavFile = WavFileReader.ReadFile(FileName)
  Dim Data As WavChunkData
  Dim Value As Short

  For Each Data In NewFile.DataChunks
    For Each Value In Data.ShortArray
      Me.DataChunks[0].ShortArray.Add(Value)
    Next
  Next

End

Public Sub MergeWavFile(FileName As String, Offset As Integer)

  Dim NewFile As WavFile = WavFileReader.ReadFile(FileName)
  Dim Data As WavChunkData
  Dim Value As Short
  Dim Position As Integer
  Dim x As Integer

  Position = CInt(Offset / 8 * 11025 * 2)
  Print Position

  For Each Data In NewFile.DataChunks
    For Each Value In Data.ShortArray

      If Me.DataChunks[0].ShortArray.Count - 1 >= Position Then
        Me.DataChunks[0].ShortArray[Position] = Combine(Me.DataChunks[0].ShortArray[Position], Value)
      Else
        For x = Me.DataChunks[0].ShortArray.Count To Position - 1
          Me.DataChunks[0].ShortArray.Add(0)
        Next
        Me.DataChunks[0].ShortArray.Add(Value)
      Endif

      Position += 1

    Next
  Next

End

Public Sub AddPause(Gap As Integer)

  Dim Index As Integer

  For Index = 0 To CInt(Gap / 8 * 11025 * 2)
    Me.DataChunks[0].ShortArray.Add(0)
  Next

End

Static Public Function Combine(Value1 As Integer, Value2 As Integer) As Short

  Dim Total As Integer = Value1 + Value2

  If Total > 32767
    Return 32767
  Else If Total < -32768
    Return -32768
  Else
    Return Total
  End If

End
