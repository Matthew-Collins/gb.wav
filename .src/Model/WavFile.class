' Gambas class file

Public GroupID As String 'Surprisingly enough, this Is Always "RIFF"
Public FileLength As Long 'File length In bytes, measured From offset 8
Public RiffType As String 'In wave files, this Is Always "WAVE"

Public FormatChunks As New WavChunkFormat[]
Public DataChunks As New WavChunkData[]
Public FactChunks As New WavChunkFact[]

Public Function NewFileLength() As Long

  Dim ReturnValue As Long
  Dim FormatChunk As WavChunkFormat
  Dim DataChunk As WavChunkData
  Dim FactChunk As WavChunkFact

  ReturnValue += 4

  For Each FormatChunk In Me.FormatChunks
    ReturnValue += 24
  Next

  For Each DataChunk In Me.DataChunks
    ReturnValue += DataChunk.NewChunkSize() + 8
  Next

  For Each FactChunk In Me.FactChunks
    ReturnValue += 12
  Next

  Return ReturnValue

End

Public Sub Mute_Stereo_Left()

  Dim DataChunk As WavChunkData
  Dim x As Integer

  For Each DataChunk In Me.DataChunks
    For x = 0 To DataChunk.ShortArray.Count - 1 Step 2
      DataChunk.ShortArray[x] = 0
    Next
  Next

End

Public Sub Mute_Stereo_Right()

  Dim DataChunk As WavChunkData
  Dim x As Integer

  For Each DataChunk In Me.DataChunks
    For x = 1 To DataChunk.ShortArray.Count - 1 Step 2
      DataChunk.ShortArray[x] = 0
    Next
  Next

End
